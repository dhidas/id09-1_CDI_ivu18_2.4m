// This PLC should contain any stops related to girder safety from the
// controls point of view.  This includes a StopAll variable that can
// be set from epics, or internally if needed


Open PLC 9


// StopAll.  0 is the all clear, anything else and we stopall
if (P_StopAllStatus != 0) {
  CMD"#*k"
}
if (P_StopAllRequest == 1) {
  P_StopAllStatus = 1
  P_StopAllRequest = -1
  CMD"#*k"
}
if (P_StopAllRequest == 0) {
  P_StopAllStatus = 0
  P_StopAllRequest = -1
}



// Check position, taper, elevation soft limits
local M1Pos
local M2Pos
local M3Pos
local M4Pos
local GAP
local TAPER
local ELEVATION
local TILT

M1Pos = Motor[1].ActPos - Motor[1].HomePos
M2Pos = Motor[2].ActPos - Motor[2].HomePos
M3Pos = Motor[3].ActPos - Motor[3].HomePos
M4Pos = Motor[4].ActPos - Motor[4].HomePos

GAP       = (M1Pos + M2Pos) / 2 / kEncCtsPerUm
TAPER     = (M2Pos - M1Pos) / kEncCtsPerUm
ELEVATION = (M3Pos + M4Pos) / 2 / kEncCtsPerUm
TILT      = (M4Pos - M3Pos) / kEncCtsPerUm

if (P_GapError == kTRUE) {
    CMD"#1,2k"
} else {
  // Buffered soft-limits, non-buffered hard limits (if you get there you are OUT, call an expert).
  if (GAP < P_MinGap - kBUFFERUM || GAP > P_MaxGap + kBUFFERUM || GAP < kMINGAP || GAP > kMAXGAP) {
    CMD"#*k"
    P_GapError = kTRUE
  }
  if (TAPER < P_MinTaper - kBUFFERUM || TAPER > P_MaxTaper + kBUFFERUM || TAPER > kMAXTAPER || TAPER < kMINTAPER) {
    CMD"#*k"
    P_GapError = kTRUE
  }

  // Check Taper limit switches if limit disable is not true
  if (inLIMITDISABLE == kFALSE && (inTAPERPLIM == kTRUE || inTAPERMLIM == kTRUE)) {
    CMD"#*k"
    P_GapError = kTRUE
  }

  // Check Taper kill switches if kill disable is not true
  if (inKILLDISABLE == kFALSE && (inTAPERPKILL == kTRUE || inTAPERMKILL == kTRUE)) {
    CMD"#*k"
    P_GapError = kTRUE
  }

}

if (P_ElevError == kTRUE) {
    CMD"#3,4k"
} else {
  // Buffered soft-limits, non-buffered hard limits (if you get there you are OUT, call an expert).
  if (ELEVATION > P_MaxElev + kBUFFERUM || ELEVATION < P_MinElev - kBUFFERUM || ELEVATION > kMAXELEV || ELEVATION < kMINELEV) {
    CMD"#*k"
    P_ElevError = kTRUE
  }
  if (TILT > P_MaxTilt + kBUFFERUM || TILT < P_MinTilt - kBUFFERUM || TILT > kMAXTILT || TILT < kMINTILT) {
    CMD"#*k"
    P_ElevError = kTRUE
  }
}



Close
