// This PLC is for waiting and executing move requests from epics.
// This is done with a PLC to ensure system is in the correct configuration
// and to manage the breaks.  The CS motor.VAL field may be used for fast
// step scanning if the breaks are released manually.

Open PLC 8

// Check all interlocks
gInterlockState = 0
gInterlockState += inUESTOP
gInterlockState += inDESTOP
if (inKILLDISABLE == kOFF) {
  gInterlockState += inGUPKILL
  gInterlockState += inGUMKILL
  gInterlockState += inGDPKILL
  gInterlockState += inGDMKILL
  gInterlockState += inEUPKILL
  gInterlockState += inEUMKILL
  gInterlockState += inEDPKILL
  gInterlockState += inEDMKILL
  gInterlockState += inTAPERPKILL
  gInterlockState += inTAPERMKILL
}



switch (gMoveMode) {
  case kMoveModeGapEleSep:

    // Check we are not in stop-all, interlock, or gap opening flag states
    if (gStopAllStatus == kFALSE && gInterlockState == kFALSE && gOpenGapExecuting == kFALSE && gHomingInProgress == kFALSE) {
      if (gGapMoveRequest == kTRUE) {
        if (Coord[2].ProgRunning == kTRUE || Coord[2].ProgActive == kTRUE) {
          Ldata.coord = 2
          ABORT
          call Timer(0.100)
        }

        // If brakes are engaged release them

        // Write setpoints to CS Q variables (same Qs are used by epics ppmac driver)
        Coord[2].Q[71] = gGapSetpoint
        Coord[2].Q[72] = gTaperSetpoint

        // Put motors in closed loop, Coordinate system 2, run program 2
        jog/1..2
        call AmpEnableDelay()
        start 2:2

        // Reset Brake timer and move request
        TIMER130BRAKES = gBrakeTimeout
        gBrakeTimerArmed = 1
        gGapMoveRequest = kFALSE
      }

      if (gElevMoveRequest == kTRUE) {
        if (Coord[3].ProgRunning == kTRUE || Coord[3].ProgActive == kTRUE) {
          Ldata.coord = 3
          ABORT
          call Timer(0.100)
        }

        // If brakes are engaged release them

        // Write setpoints to CS Q variables (same Qs are used by epics ppmac driver)
        Coord[3].Q[73] = gElevSetpoint
        Coord[3].Q[74] = gTiltSetpoint

        // Put motors in closed loop, Coordinate system 3, run program 3
        jog/3..4
        call AmpEnableDelay()
        start 3:3

        // Reset Brake timer and move request
        TIMER130BRAKES = gBrakeTimeout
        gBrakeTimerArmed = 1
        gElevMoveRequest = kFALSE
      }

    }
    break;
  case kMoveModeGapEleComb:

    // Check we are not in stop-all, interlock, or gap opening flag states
    if (gStopAllStatus == kFALSE && gInterlockState == kFALSE && gOpenGapExecuting == kFALSE && gHomingInProgress == kFALSE) {
      if (gGapElevMoveRequest == kTRUE) {
        if (Coord[4].ProgRunning == kTRUE || Coord[4].ProgActive == kTRUE) {
          Ldata.coord = 4
          ABORT
          call Timer(0.100)
        }

        // If brakes are engaged release them

        // Write setpoints to CS Q variables (same Qs are used by epics ppmac driver)
        Coord[4].Q[71] = gGapSetpoint
        Coord[4].Q[72] = gTaperSetpoint
        Coord[4].Q[73] = gElevSetpoint
        Coord[4].Q[74] = gTiltSetpoint

        // Put motors in closed loop, Coordinate system 2, run program 2
        jog/1..4
        call AmpEnableDelay()
        start 4:4

        // Reset Brake timer and move request
        TIMER130BRAKES = gBrakeTimeout
        gBrakeTimerArmed = 1
        gGapElevMoveRequest = kFALSE
      }

    }
    break;
  default:
    // Ignore move requests when in any other mode
    break;
}





Close
