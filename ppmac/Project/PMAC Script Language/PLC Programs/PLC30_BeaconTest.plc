// This PLC is to test the beacon lights

Open PLC 30

disable plc 7

P3000 = P3000 + 1

local lColor
local lCount
local lOverlap
local lDelay1
local lDelay2

lCount = 7
lOverlap = 0.02
lDelay1 = 0.1
lDelay2 = 0.05


outBEACONGREEN = kOFF
outBEACONYELLOW = kOFF
outBEACONRED = kOFF

call Timer(0.5)

while (lCount > 0) {

  lColor = 4
  while (lColor > 0) {
    switch (lColor) {
      case 4:
        outBEACONGREEN = kON
        call Timer(lOverlap)
        outBEACONYELLOW = kOFF
        break
      case 3:
        outBEACONYELLOW = kON
        call Timer(lOverlap)
        outBEACONGREEN = kOFF
        break
      case 2:
        outBEACONRED = kON
        call Timer(lOverlap)
        outBEACONYELLOW = kOFF
        break
      case 1:
        outBEACONYELLOW = kON
        call Timer(lOverlap)
        outBEACONRED = kOFF
        break
    }
    call Timer(lDelay1)
    lColor--
  }
  lCount--
}

lCount = 20
while (lCount > 0) {
  outBEACONGREEN = lCount % 2
  outBEACONYELLOW = lCount % 2
  outBEACONRED = lCount % 2
  lCount--
  call Timer(lDelay2)
}

outBEACONGREEN = kOFF
outBEACONYELLOW = kOFF
outBEACONRED = kOFF

disable plc 30
enable plc 7
Close
